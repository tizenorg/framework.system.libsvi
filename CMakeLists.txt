CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(feedback C)

SET(PREFIX ${CMAKE_INSTALL_PREFIX})
SET(EXEC_PREFIX "\${prefix}")
SET(LIBDIR "\${prefix}/lib")
SET(INCLUDEDIR "\${prefix}/include/${PROJECT_NAME}")
SET(VERSION 0.1.4)

IF("${DEVICE}" STREQUAL "wearable")
	OPTION(USE_WEARABLE "Use Wearable" ON)
ELSEIF("${DEVICE}" STREQUAL "mobile")
	OPTION(USE_MOBILE "Use Mobile" ON)
ENDIF()

SET(FEEDBACK_DATA_PATH ${CMAKE_CURRENT_SOURCE_DIR}/data)
IF(USE_WEARABLE)
	SET(FEEDBACK_DATA_DIRS ${FEEDBACK_DATA_PATH}/wearable)
ELSEIF(USE_MOBILE)
	SET(FEEDBACK_DATA_DIRS ${FEEDBACK_DATA_PATH}/mobile)
ENDIF()

SET(SRCS
	src/devices.c
	src/sound.c
	src/vibrator.c
	src/xmlparser.c
	src/feedback.c
	src/str.c)

IF(NOT USE_MOBILE)
SET(SRCS ${SRCS}
	src/dbus.c)
ENDIF()

IF(FEATURE_LED_ENABLE)
SET(SRCS ${SRCS}
	src/led.c)
ENDIF()

SET(HEADERS
	SLP_FEEDBACK_PG.h
	include/feedback.h
	include/feedback-ids.h)

SET(SVI_NAME svi)

SET(SVI_HEADERS
	include/svi.h
	include/svi-ids.h)

SET(SVI_SRCS
	src/svi.c)

SET(DEPENDENTS "vconf mm-keysound dlog libxml-2.0 glib-2.0")
SET(PC_DEPENDENTS "capi-base-common")

IF(USE_MOBILE)
SET(DEPENDENTS ${DEPENDENTS} haptic)
ELSE()
SET(DEPENDENTS ${DEPENDENTS} dbus-1)
ENDIF()

SET(PC_NAME ${PROJECT_NAME})
SET(PC_REQUIRED ${PC_DEPENDENTS})
SET(PC_LDFLAGS -l${PROJECT_NAME})

INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/include)

INCLUDE(FindPkgConfig)
pkg_check_modules(pkgs REQUIRED ${DEPENDENTS})

FOREACH(flag ${pkgs_CFLAGS})
	SET(EXTRA_CFLAGS "${EXTRA_CFLAGS} ${flag}")
ENDFOREACH(flag)

SET(EXTRA_CFLAGS "${EXTRA_CFLAGS} -fvisibility=hidden")
SET(EXTRA_CFLAGS "${EXTRA_CFLAGS} -g")
SET(EXTRA_CFLAGS "${EXTRA_CFLAGS} -Werror")

SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${EXTRA_CFLAGS}")

IF(USE_MOBILE)
	ADD_DEFINITIONS("-DMOBILE")
ENDIF()

ADD_LIBRARY(${PROJECT_NAME} SHARED ${SRCS})
SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES SOVERSION 0)
SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES VERSION ${VERSION})
TARGET_LINK_LIBRARIES(${PROJECT_NAME} ${pkgs_LDFLAGS})

ADD_LIBRARY(${SVI_NAME} SHARED ${SVI_SRCS})
SET_TARGET_PROPERTIES(${SVI_NAME} PROPERTIES SOVERSION 0)
SET_TARGET_PROPERTIES(${SVI_NAME} PROPERTIES VERSION ${VERSION})
TARGET_LINK_LIBRARIES(${SVI_NAME} -L${CMAKE_BINARY_DIR} -lfeedback)
TARGET_LINK_LIBRARIES(${SVI_NAME} ${pkgs_LDFLAGS})

CONFIGURE_FILE(${PROJECT_NAME}.pc.in ${PROJECT_NAME}.pc @ONLY)
CONFIGURE_FILE(${SVI_NAME}.pc.in ${SVI_NAME}.pc @ONLY)

FOREACH(hfile ${HEADERS})
	INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/${hfile} DESTINATION include/${PROJECT_NAME})
ENDFOREACH(hfile)

FOREACH(hfile ${SVI_HEADERS})
	INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/${hfile} DESTINATION include/${SVI_NAME})
ENDFOREACH(hfile)

INSTALL(DIRECTORY ${FEEDBACK_DATA_DIRS}/ DESTINATION share/${PROJECT_NAME})

INSTALL(TARGETS ${PROJECT_NAME} DESTINATION lib COMPONENT RuntimeLibraries)
INSTALL(TARGETS ${SVI_NAME} DESTINATION lib COMPONENT RuntimeLibraries)
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}.pc DESTINATION lib/pkgconfig)
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/${SVI_NAME}.pc DESTINATION lib/pkgconfig)
